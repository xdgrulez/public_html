<html lang="en">
<head>
<title>Order1 - Manual of the XDG Development Kit</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Manual of the XDG Development Kit">
<meta name="generator" content="makeinfo 4.8">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="Principles-list.html#Principles-list" title="Principles list">
<link rel="prev" href="Order.html#Order" title="Order">
<link rel="next" href="Order1Constraints.html#Order1Constraints" title="Order1Constraints">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<p>
<a name="Order1"></a>
Next:&nbsp;<a rel="next" accesskey="n" href="Order1Constraints.html#Order1Constraints">Order1Constraints</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="Order.html#Order">Order</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Principles-list.html#Principles-list">Principles list</a>
<hr>
</div>

<h4 class="subsection">7.2.49 Order1 principle</h4>

     <ul>
<li>identifier: <code>principle.order1</code>
<li>dimension variables: <code>D</code>
<li>argument variables:<br>
<code>On: iset(label(D))</code><br>
<code>Order: list(set(label(D)))</code><br>
<code>Yields: bool</code>
<li>default values:<br>
<code>On: _.D.entry.on</code><br>
<code>Order: []</code><br>
<code>Yields: false</code>
<li>model record:
     <pre class="example">          { selfSet:  vec(label(D) ints)
            self:     label(D)
            pos:      ints
            yield:    ints
            yieldS:   ints
            yieldL:   vec(label(D) ints) }
     </pre>
     <li>constraints: <code>OrderMakeNodes</code> (priority 130),
<code>Order1Conditions</code> (120), and <code>OrderDist</code> (90)
<li>edge constraints: none
</ul>

   <p>This principle is a generalization of the original Order principle
(<a href="Order.html#Order">Order</a>). Contrary to that, it the <code>Order</code> argument variable
specifies an order on sets of labels, instead of just labels.

   <p>Here is the definition of the <code>Order1Conditions</code> constraint functor:
<pre class="verbatim">%% Copyright 2001-2008
%% by Ralph Debusmann &lt;rade@ps.uni-sb.de> (Saarland University) and
%%    Denys Duchier &lt;duchier@ps.uni-sb.de> (LIFO, Orleans) and
%%    Jorge Marques Pelizzoni &lt;jpeliz@icmc.usp.br> (ICMC, Sao Paulo) and
%%    Jochen Setz &lt;info@jochensetz.de> (Saarland University)
%%
functor
import
%   System(showError show)
   
   Helpers(checkModel) at 'Helpers.ozf'
export
   Constraint
define
   proc {Constraint Nodes G Principle FD FS Select}
      DVA2DIDA = Principle.dVA2DIDA
      ArgRecProc = Principle.argRecProc
      %%
      DIDA = {DVA2DIDA 'D'}
   in
      %% check features
      if {Helpers.checkModel 'Order1Conditions.oz' Nodes
	  [DIDA#daughters
	   DIDA#daughtersL]} then
	 %% get label lattice LabelLat
	 DIDA2LabelLat = G.dIDA2LabelLat
	 LabelLat = {DIDA2LabelLat DIDA}
	 LAs = LabelLat.constants
	 LCardI = LabelLat.card
	 %%
	 Models = {Map Nodes fun {$ Node} Node.DIDA.model end}
	 %%
	 YieldMs = {Map Models
		    fun {$ Model} Model.yield end}
	 %%
	 PosMs = {Map Nodes
		  fun {$ Node} Node.pos end}
      in
	 for Node in Nodes do
	    Model = Node.DIDA.model
	    %% yield(v) = union{ pos(v') | v' in eqdown(v) }
	    Model.yield = {Select.union PosMs Model.eqdown}
	    %% yieldS(v) = union{ pos(v') | v' in down(v) }
	    Model.yieldS = {Select.union PosMs Model.down}
	    %% yieldS(v) = union{ yield(v') | v' in daughters(v) }
	    Model.yieldS = {Select.union YieldMs Model.daughters}	 
	    for LA in LabelLat.constants do
	       %% yield_l(v) = union{ yield(v') | v' in daughtersL_l(v) }
	       Model.yieldL.LA = {Select.union YieldMs Model.daughtersL.LA}
	       %% |daughtersL_l(v)| =&lt; |yieldL_l(v)|
	       {FD.lesseq
		{FS.card Model.daughtersL.LA}
		{FS.card Model.yieldL.LA}}
	    end
	    %% yieldS(v) = union{ yield_l(v) | l in labels }
	    %% i.e. the union of the l-yields of v are its strict yield
	    Model.yieldS = {FS.unionN Model.yieldL}
	    %% yield(v) = pos(v) union yieldS(v)
	    Model.yield = {FS.unionN [Node.pos Model.yieldS]}
	    %% get ordered list of projections:
	    %% ProjLMs describes
	    %% proj: V -> (L -> 2^Pos) (Pos is the set of positions), where:
	    %% 1) if YieldsB==true then
	    %%   proj(v)(l) = yieldL(v)(l) union selfSet(v)(l)
	    %% 2) if YieldsB==false then
	    %%   proj(v)(l) = union{ pos(v') | v' in daughters(v)(l) } union
	    %%                selfSet(v)(l)
	    OrderLMs = {ArgRecProc 'Order' o('_': Node)}
	    OrderLM = {FS.unionN OrderLMs}
	    OrderLCardI = {FS.card OrderLM}
	    YieldsB = {ArgRecProc 'Yields' o('_': Node)}==2
	    ProjLMs =
	    {Map LAs
	     fun {$ LA}
		M =
		if YieldsB then
		   Model.yieldL.LA
		else
		   DaughtersLM = Model.daughtersL.LA
		   PosDaughtersLM = {Select.union PosMs DaughtersLM}
		in
		   {FD.equal
		    {FS.card PosDaughtersLM}
		    {FS.card DaughtersLM}}
		   %%
		   PosDaughtersLM
		end
	     in
		{FS.union M Model.selfSet.LA}
	     end}
	    %% get ordered list of the projections according to the
	    %% total order
	    OrderProjLMs =
	    {Map OrderLMs
	     fun {$ LM} {Select.union ProjLMs LM} end}
	 in
	    %% order the projections list
	    if YieldsB andthen OrderLCardI==LCardI then
	       {Select.seqUnion OrderProjLMs Model.yield}
	    else
	       {FS.int.seq OrderProjLMs}
	    end
	 end
      end
   end
end
</pre>
<!-- ************************************************************ -->

   </body></html>

