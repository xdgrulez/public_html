<html lang="en">
<head>
<title>Order2 - Manual of the XDG Development Kit</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Manual of the XDG Development Kit">
<meta name="generator" content="makeinfo 4.8">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="Principles-list.html#Principles-list" title="Principles list">
<link rel="prev" href="Order1Dist.html#Order1Dist" title="Order1Dist">
<link rel="next" href="Order2Constraints.html#Order2Constraints" title="Order2Constraints">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<p>
<a name="Order2"></a>
Next:&nbsp;<a rel="next" accesskey="n" href="Order2Constraints.html#Order2Constraints">Order2Constraints</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="Order1Dist.html#Order1Dist">Order1Dist</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Principles-list.html#Principles-list">Principles list</a>
<hr>
</div>

<h4 class="subsection">7.2.52 Order2 principle</h4>

     <ul>
<li>identifier: <code>principle.order2</code>
<li>dimension variables: <code>D</code>
<li>argument variables:<br>
<code>Order: set(tuple(label(D)|{"^"} label(D)|{"^"}))</code><br>
<code>Yields: bool</code>
<li>default values:<br>
<code>Order: _.D.entry.order</code><br>
<code>Yields: false</code>
<li>model record:
     <pre class="example">          { pos:      ints
            yield:    ints
            yieldS:   ints
            yieldL:   vec(label(D) ints) }
     </pre>
     <li>constraints: <code>Order2MakeNodes</code> (priority 130),
<code>Order2Conditions</code> (120), and <code>Order2Dist</code> (90)
<li>edge constraints: none
</ul>

   <p>This is the new lexicalized order principle used also in the thesis.

   <p>Here is the definition of the <code>Order2MakeNodes</code> constraint functor:
<pre class="verbatim">%% Copyright 2001-2008
%% by Ralph Debusmann &lt;rade@ps.uni-sb.de> (Saarland University) and
%%    Denys Duchier &lt;duchier@ps.uni-sb.de> (LIFO, Orleans) and
%%    Jorge Marques Pelizzoni &lt;jpeliz@icmc.usp.br> (ICMC, Sao Paulo) and
%%    Jochen Setz &lt;info@jochensetz.de> (Saarland University)
%%
functor
import
   Helpers(checkModel) at 'Helpers.ozf'
export
   Constraint
prepare
   RecordForAll = Record.forAll
define
   proc {Constraint Nodes G Principle FD FS Select}
      DVA2DIDA = Principle.dVA2DIDA
      %%
      DIDA = {DVA2DIDA 'D'}
   in
      %% check features
      if {Helpers.checkModel 'Order2MakeNodes.oz' Nodes
	  [DIDA#eqdown]} then
	 %% get node set NodeSetM
	 NodeSetM = Nodes.1.nodeSet
      in
	 for Node in Nodes do
	    Model = Node.DIDA.model
	    %%
	    {FS.subset Model.yield NodeSetM}
	    {FS.subset Model.yieldS NodeSetM}
	    {RecordForAll Model.yieldL proc {$ M} {FS.subset M NodeSetM} end}
	 in
  	    %% |yield(v)| = |eqdown(v)|
	    {FD.equal
	     {FS.card Model.yield}
	     {FS.card Model.eqdown}}
	 end
      end
   end
end
</pre>
Here is the definition of the <code>Order2Conditions</code> constraint functor:
<pre class="verbatim">%% Copyright 2001-2008
%% by Ralph Debusmann &lt;rade@ps.uni-sb.de> (Saarland University) and
%%    Denys Duchier &lt;duchier@ps.uni-sb.de> (LIFO, Orleans) and
%%    Jorge Marques Pelizzoni &lt;jpeliz@icmc.usp.br> (ICMC, Sao Paulo) and
%%    Jochen Setz &lt;info@jochensetz.de> (Saarland University)
%%
functor
import
%   System(show)
   
   Domain(make) at '../../../Compiler/Lattices/Domain.ozf'
   Tuple1(make) at '../../../Compiler/Lattices/Tuple.ozf'

   Helpers(checkModel) at 'Helpers.ozf'
   Opti(isIn) at 'Opti.ozf'
export
   Constraint
define
   proc {Constraint Nodes G Principle FD FS Select}
      DVA2DIDA = Principle.dVA2DIDA
      ArgRecProc = Principle.argRecProc
      %%
      DIDA = {DVA2DIDA 'D'}
   in
      %% check features
      if {Helpers.checkModel 'Order2Conditions.oz' Nodes
	  [DIDA#daughters
	   DIDA#daughtersL]} then
	 %% get label lattice LabelLat
	 DIDA2LabelLat = G.dIDA2LabelLat
	 LabelLat = {DIDA2LabelLat DIDA}
	 LAs = LabelLat.constants
	 %%
	 LabelPairLat = {Tuple1.make [LabelLat LabelLat]}
	 %%
	 NodeSetM = Nodes.1.nodeSet
	 %%
	 Models = {Map Nodes fun {$ Node} Node.DIDA.model end}
	 %%
	 YieldMs = {Map Models
		    fun {$ Model} Model.yield end}
	 %%
	 PosMs = {Map Nodes
		  fun {$ Node} Node.pos end}
	 %%
	 fun {GetYield Model LA YieldsB}
	    if YieldsB then
	       Model.yieldL.LA
	    else
	       DaughtersLM = Model.daughtersL.LA
	       PosDaughtersLM = {Select.union PosMs DaughtersLM}
	       {FD.equal
		{FS.card PosDaughtersLM}
		{FS.card DaughtersLM}}
	    in
	       PosDaughtersLM
	    end
	 end
      in
	 for Node in Nodes do
	    OrderM = {ArgRecProc 'Order' o('_': Node)}
	    YieldsB = {ArgRecProc 'Yields' o('_': Node)}==2
	    %%
	    Model = Node.DIDA.model
	    %% yieldS(v) = union{ yield(v') | v' in daughters(v) }
	    Model.yieldS = {Select.union YieldMs Model.daughters}	 
	    %% yield(v) = pos(v) union yieldS(v)
	    Model.yield = {FS.unionN [Node.pos Model.yieldS]}
	 in
	    for LA in LAs do
	       %% yield_l(v) = union{ yield(v') | v' in daughtersL_l(v) }
	       Model.yieldL.LA = {Select.union YieldMs Model.daughtersL.LA}
	       %% |daughtersL_l(v)| =&lt; |yieldL_l(v)|
	       {FD.lesseq
		{FS.card Model.daughtersL.LA}
		{FS.card Model.yieldL.LA}}
	    end
	    %% yieldS(v) = union{ yield_l(v) | l in labels }
	    %% i.e. the union of the l-yields of v are its strict yield
	    {FS.unionN Model.yieldL Model.yieldS}
	    %%
	    {FS.equal Model.daughtersL.'^' FS.value.empty}
	    %%
	    for LA1 in LAs do
	       for LA2 in LAs do
		  I = {LabelPairLat.aIs2I [LA1 LA2]}
	       in
		  if {Not {Opti.isIn I OrderM}=='out'} then
		     Ms =
		     if LA1=='^' andthen LA2=='^' then
			nil
		     elseif LA1=='^' then
			[Node.pos {GetYield Model LA2 YieldsB}]
		     elseif LA2=='^' then 
			[{GetYield Model LA1 YieldsB} Node.pos]
		     else
			[{GetYield Model LA1 YieldsB} {GetYield Model LA2 YieldsB}]
		     end
		     %%
		     Ms1 = {Map Ms
			    fun {$ M}
			       M1 = {FS.subset $ NodeSetM}
			    in
			       {FD.impl
				{FS.reified.include I OrderM}
				{FS.reified.equal M M1} 1}
			       %%
			       {FD.impl
				{FD.nega
				 {FS.reified.include I OrderM}}
				{FS.reified.equal M1 FS.value.empty} 1}
			       M1
			    end}
		  in
		     if {Not Ms==nil} then {FS.int.seq Ms1} end
		  end
	       end
	    end
	 end
      end
   end
end
</pre>
Here is the definition of the <code>Order2Dist</code> constraint functor:
<pre class="verbatim">%% Copyright 2001-2008
%% by Ralph Debusmann &lt;rade@ps.uni-sb.de> (Saarland University) and
%%    Denys Duchier &lt;duchier@ps.uni-sb.de> (LIFO, Orleans) and
%%    Jorge Marques Pelizzoni &lt;jpeliz@icmc.usp.br> (ICMC, Sao Paulo) and
%%    Jochen Setz &lt;info@jochensetz.de> (Saarland University)
%%
functor
import
%   Space(waitStable)

   Distributor(distributeDs distributeMs) at 'Distributor.ozf'
   Helpers(checkModel) at 'Helpers.ozf'
export
   Constraint
define
   proc {Constraint Nodes G Principle FD FS Select}
      DVA2DIDA = Principle.dVA2DIDA
      %%
      DIDA = {DVA2DIDA 'D'}
   in
      %% check features
      if {Helpers.checkModel 'Order2Dist.oz' Nodes
	  [DIDA#pos]} then
	 %% distribute pos
	 PosMs = {Map Nodes
		   fun {$ Node} Node.DIDA.model.pos end}
      in
	 {Distributor.distributeMs PosMs FS}
      end
   end
end
</pre>
<!-- ************************************************************ -->

   </body></html>

