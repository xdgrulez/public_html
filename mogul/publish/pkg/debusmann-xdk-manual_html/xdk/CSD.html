<html lang="en">
<head>
<title>CSD - Manual of the XDG Development Kit</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Manual of the XDG Development Kit">
<meta name="generator" content="makeinfo 4.8">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="Grammars.html#Grammars" title="Grammars">
<link rel="prev" href="Benoit.html#Benoit" title="Benoit">
<link rel="next" href="CSDPW.html#CSDPW" title="CSDPW">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<p>
<a name="CSD"></a>
Next:&nbsp;<a rel="next" accesskey="n" href="CSDPW.html#CSDPW">CSDPW</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="Benoit.html#Benoit">Benoit</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Grammars.html#Grammars">Grammars</a>
<hr>
</div>

<h3 class="section">5.9 CSD</h3>

<p>This grammar covers the language of n ns followed by n
vs, where the ith n depends on the ith v, using the
special principle <code>principle.csd</code> (<a href="CSD1.html#CSD1">CSD1</a>). It is a demo
grammar for cross-serial dependencies.  It was written by Ralph
Debusmann for his dissertation.

<pre class="verbatim">usedim id
usedim lp
usedim idlp
usedim lex
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define id dimension
defdim id {
  deftype "id.label" {n v}
  deflabeltype "id.label"
  defentrytype {in: valency("id.label")
                out: valency("id.label")}
%%
  useprinciple "principle.graph" {
    dims {D: id}}
  useprinciple "principle.tree" {
    dims {D: id}}
  useprinciple "principle.valency" {
    dims {D: id}}
  useprinciple "principle.csd" {
    dims {D: id} 
    args {NounLabels: {n}}}
%%
  output "output.pretty"
}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define lp dimension
defdim lp {
  deftype "lp.label" {"1" "2" "^"}
  deflabeltype "lp.label"
  defentrytype {in: valency("lp.label")
                out: valency("lp.label")
                order: set(tuple("lp.label" "lp.label"))}
%%
  useprinciple "principle.graph" {
    dims {D: lp}}
  useprinciple "principle.tree" {
    dims {D: lp}}
  useprinciple "principle.valency" {
    dims {D: lp}}
  useprinciple "principle.order2" {
    dims {D: lp}}
%%
  output "output.pretty"
}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define idlp dimension
defdim idlp {
  useprinciple "principle.climbing" {
    dims {D1: lp
          D2: id}
    args {Subgraphs: false}}
%%
  output "output.pretty"
}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define lex dimension
defdim lex {
  defentrytype {word: string}
%%
  output "output.dags1"
  output "output.latexs1"
  useoutput "output.dags1"
}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define lexicon

defentry {
  dim id {in: {}
          out: {n! v?}}
  dim lp {in: {}
          out: {"1"* "2"?}
          order: &lt;"1" "^" "2">}
  dim lex {word: "v"}}

defentry {
  dim id {in: {v!}
          out: {n! v?}}
  dim lp {in: {"2"!}
          out: {"2"?}
          order: &lt;"^" "2">}
  dim lex {word: "v"}}

defentry {
  dim id {in: {n!}}
  dim lp {in: {"1"!}}
  dim lex {word: "n"}}
</pre>
<!-- ************************************************************ -->

   </body></html>

