<html lang="en">
<head>
<title>SAT - Manual of the XDG Development Kit</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Manual of the XDG Development Kit">
<meta name="generator" content="makeinfo 4.8">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="Grammars.html#Grammars" title="Grammars">
<link rel="prev" href="Negation.html#Negation" title="Negation">
<link rel="next" href="SATPW.html#SATPW" title="SATPW">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<p>
<a name="SAT"></a>
Next:&nbsp;<a rel="next" accesskey="n" href="SATPW.html#SATPW">SATPW</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="Negation.html#Negation">Negation</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Grammars.html#Grammars">Grammars</a>
<hr>
</div>

<h3 class="section">5.32 SAT</h3>

<p>This is a grammar representing a reduction of the NP-complete SAT
problem to XDG parsing, using the special principle
<code>principle.pl</code> (<a href="PL.html#PL">PL</a>).  It was written by Ralph Debusmann and
Gert Smolka, and is featured in Ralph Debusmann's dissertation and the
paper <cite>Multi-dimensional Dependency Grammar as Multigraph
Description</cite> (<a href="References.html#References">References</a>).

<pre class="verbatim">usedim pl
usedim lex
%% %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define dimension pl
defdim pl {
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% define types
  deftype "pl.label" {arg1 arg2 bar "^"}
  deflabeltype "pl.label"
  defattrstype {truth: bool
                bars: int}
  defentrytype {in: valency("pl.label")
                out: valency("pl.label")
                order: set(tuple("pl.label" "pl.label"))}
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% use principles
  useprinciple "principle.graph" {
    dims {D: pl}}
  useprinciple "principle.tree" {
    dims {D: pl}}
  useprinciple "principle.valency" {
    dims {D: pl}}
  useprinciple "principle.order2" {
    dims {D: pl}}
  useprinciple "principle.projectivity" {
    dims {D: pl}}
  useprinciple "principle.pl" {
    dims {D: pl}}
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% use and choose outputs
  output "output.pretty"
}
%% %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define dimension lex
defdim lex {
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% define types
  defentrytype {word: string}
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% use principles
  useprinciple "principle.entries" {}
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% use and choose outputs
  output "output.dags1"
  output "output.latexs1"
  useoutput "output.dags1"
}
%% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define lexicon
%% i.e., the ordered fragments

%% implications
defentry {
  dim pl {in: {arg1? arg2?}
          out: {arg1! arg2!}
          order: &lt;"^" arg1 arg2>}
  dim lex {word: "impl"}}

%% zeros
defentry {
  dim pl {in: {arg1? arg2?}}
  dim lex {word: "0"}}

%% variables
defentry {
  dim pl {in: {arg1? arg2?}
          out: {bar!}
          order: &lt;"^" bar>} 
  dim lex {word: "var"}}

%% bars
defentry {
  dim pl {in: {bar!}
          out: {bar?}
          order: &lt;"^" bar>}
  dim lex {word: "I"}}
</pre>
<!-- ************************************************************ -->

   </body></html>

