<html lang="en">
<head>
<title>Acl01 - Manual of the XDG Development Kit</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Manual of the XDG Development Kit">
<meta name="generator" content="makeinfo 4.8">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="Grammars.html#Grammars" title="Grammars">
<link rel="prev" href="ANBNPW.html#ANBNPW" title="ANBNPW">
<link rel="next" href="Acl01PW.html#Acl01PW" title="Acl01PW">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<p>
<a name="Acl01"></a>
Next:&nbsp;<a rel="next" accesskey="n" href="Acl01PW.html#Acl01PW">Acl01PW</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="ANBNPW.html#ANBNPW">ANBNPW</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Grammars.html#Grammars">Grammars</a>
<hr>
</div>

<h3 class="section">5.5 Acl01</h3>

<p>This grammar covers German subordinate clauses and word order
variation therein. It uses the two TDG graph dimensions ID and LP, and
was written by Denys Duchier and Ralph Debusmann, for their ACL 2001
paper <cite>Topological Dependency Trees: A Constraint-Based Account
of Linear Precedence</cite> (<a href="References.html#References">References</a>), and is described therein.

<pre class="verbatim">usedim id
usedim lp
usedim idlp
usedim lex
%% %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define dimension id
defdim id {
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% define types
  deftype "id.label" {det subj obj vbse vprt vinf prt}
  deftype "id.person" {first second third}
  deftype "id.number" {sg pl}
  deftype "id.gender" {masc fem neut}
  deftype "id.case" {nom gen dat acc}
  deftype "id.def" {def indef undef}
  deftype "id.agr" tuple("id.person" "id.number" "id.gender" "id.case" "id.def")
  deftype "id.agrs" iset("id.agr")

  deflabeltype "id.label"
  defattrstype {agr: "id.agr"}
  defentrytype {in: valency("id.label")
                out: valency("id.label")
                agrs: "id.agrs"
                agree: set("id.label")
                govern: vec("id.label" "id.agrs")}
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% use principles
  useprinciple "principle.graph" {
    dims {D: id}}
  useprinciple "principle.tree" {
    dims {D: id}}
  useprinciple "principle.valency" {
    dims {D: id}}
  useprinciple "principle.agr" {
    dims {D: id}
    args {Agr: _.D.attrs.agr
          Agrs: _.D.entry.agrs}}
  useprinciple "principle.agreement" {
    dims {D: id}
    args {Agr1: ^.D.attrs.agr
          Agr2: _.D.attrs.agr
          Agree: ^.D.entry.agree}}
  useprinciple "principle.government" {
    dims {D: id}
    args {Agr2: _.D.attrs.agr
          Govern: ^.D.entry.govern}}
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% use and choose outputs
  output "output.dag"
  output "output.latex"
  output "output.decode"
  output "output.pretty"
  output "output.xml"
  output "output.xml1"
  output "output.xml2"
  output "output.xml3"
  output "output.xml4"
}
%% %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define dimension lp
defdim lp {
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% define types
  deftype "lp.label" {d df n mf vcf p pf v vxf}
  deflabeltype "lp.label"
  defentrytype {in: valency("lp.label")
                out: valency("lp.label")
                on: iset("lp.label")
}
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% use principles
  useprinciple "principle.graph" {
    dims {D: lp}}
  useprinciple "principle.tree" {
    dims {D: lp}}
  useprinciple "principle.valency" {
    dims {D: lp}}
  useprinciple "principle.order" {
    dims {D: lp}
    args {Order: [d df n mf vcf p pf v vxf]
          On: _.D.entry.on
          Yields: true}}
  useprinciple "principle.projectivity" {
    dims {D: lp}}
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% use and choose outputs
  output "output.dag"
  output "output.latex"
  output "output.decode"
  output "output.pretty"
}
%% %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define dimension idlp
defdim idlp {
  defentrytype {blocks: set("id.label")
                end: vec("lp.label" set("id.label"))}
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% use principles
  useprinciple "principle.climbing" {
    dims {D1: lp
          D2: id}}
  useprinciple "principle.barriers" {
    dims {D1: lp
          D2: id
          D3: idlp}
    args {Blocks: _.D3.entry.blocks}}
  useprinciple "principle.linkingEnd" {
    dims {D1: lp
          D2: id
          D3: idlp}
    args {End: ^.D3.entry.end}}
}
%% %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define dimension lex
defdim lex {
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% define types
  defentrytype {word: string}
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% use principles
  useprinciple "principle.entries" {}
  %% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% use and choose outputs
  output "output.dags1"
  output "output.latexs1"
  useoutput "output.dags1"
}
%% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define classes
defclass "det" Word Agrs {
  dim id {in: {det?}
          agrs: Agrs
          govern: {det: $ ()}}
  dim lp {in: {df?}
          on: {d}}
  dim lex {word: Word}}

defclass "cnoun" Word Agrs {
  dim id {in: {subj? obj?}
          out: {det?}
          agrs: Agrs
          agree: {det}}
  dim lp {in: {mf?}
          on: {n}
          out: {df?}}
  dim idlp {blocks: {det}
            end: {df: {det}}}
  dim lex {word: Word}}

defclass "pname" Word Agrs {
  dim id {in: {subj? obj?}
          agrs: Agrs}
  dim lp {in: {mf?}
          on: {n}}
  dim lex {word: Word}}

defclass "fin" Word Agrs {
  dim id {in: {}
          out: {subj}
          agrs: Agrs
          agree: {subj}
          govern: {subj: $ nom}}
  dim lp {in: {}
          out: {mf* vxf?}
          on: {v}}
  dim idlp {blocks: bot}
  dim lex {word: Word}}

defclass "can" Word {
  dim lp {in: {vcf?}
          on: {v}}
  dim lex {word: Word}}

defclass "noncan" Word {
  dim lp {in: {vxf?}
          on: {v}
          out: {mf* vxf?}}
  dim lex {word: Word}}

defclass "ersatz" {
  dim id {in: {vbse? vprt?}}}

defclass "vbse" {
  dim id {in: {vbse?}}}

defclass "vinf" {
  dim id {in: {vinf?}
          out: {prt}}
  dim lp {out: {pf}}}

defclass "vprt" {
  dim id {in: {vprt?}}}

defclass "tr" {
  dim id {out: {obj}
          govern: {obj: $ acc}}}

defclass "vbsec" {
  dim id {out: {vbse}}
  dim lp {out: {vcf?}}}

defclass "vprtc" {
  dim id {out: {vprt}}
  dim lp {out: {vcf?}}}

defclass "vinfc" {
  dim id {out: {vinf}}
  dim lp {out: {vcf?}}}

defclass "prt" Word {
  dim id {in: {prt?}}
  dim lp {in: {pf?}
          on: {p}}
  dim lex {word: Word}}

%% %%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% define entries
% Determiners
defentry {
  "det" {Agrs: ($ masc &amp; nom &amp; sg &amp; indef) | ($ neut &amp; (nom|acc) &amp; sg &amp; indef)
         Word: "ein"}}
defentry {
  "det" {Agrs: ($ masc &amp; acc &amp; sg &amp; indef)
         Word: "einen"}}
defentry {
  "det" {Agrs: ($ (masc|neut) &amp; dat &amp; sg &amp; indef)
         Word: "einem"}}
defentry {
  "det" {Agrs: ($ (masc|neut) &amp; gen &amp; sg &amp; indef)
         Word: "eines"}}
defentry {
  "det" {Agrs: ($ fem &amp; (nom|acc) &amp; sg &amp; indef)
         Word: "eine"}}
defentry {
  "det" {Agrs: ($ fem &amp; (dat|gen) &amp; sg &amp; indef)
         Word: "einer"}}

defentry {
  "det" {Agrs: (($ masc &amp; nom &amp; sg &amp; def) | ($ fem &amp; (dat|gen) &amp; sg &amp; def) |
                ($ gen &amp; pl &amp; def))
         Word: "der"}}
defentry {
  "det" {Agrs: ($ masc &amp; acc &amp; sg &amp; def) | ($ dat &amp; pl &amp; def)
         Word: "den"}}
defentry {
  "det" {Agrs: ($ (masc|neut) &amp; dat &amp; sg &amp; def)
         Word: "dem"}}
defentry {
  "det" {Agrs: ($ (masc|neut) &amp; gen &amp; sg &amp; def)
         Word: "des"}}
defentry {
  "det" {Agrs: ($ neut &amp; (nom|acc) &amp; sg &amp; def)
         Word: "das"}}
defentry {
  "det" {Agrs: ($ fem &amp; (nom|acc) &amp; sg &amp; def) | ($ (nom|acc) &amp; pl &amp; def)
         Word: "die"}}

% Common nouns
defentry {
  "cnoun" {Agrs: ($ fem &amp; third &amp; sg)
           Word: "frau"}}
defentry {
  "cnoun" {Agrs: ($ fem &amp; third &amp; pl)
           Word: "frauen"}}

defentry {
  "cnoun" {Agrs: ($ masc &amp; third &amp; sg &amp; (nom|dat|acc))
           Word: "mann"}}
defentry {
  "cnoun" {Agrs: ($ masc &amp; third &amp; sg &amp; dat)
           Word: "manne"}}
defentry {
  "cnoun" {Agrs: ($ masc &amp; third &amp; sg &amp; gen)
           Word: "mannes"|"manns"}}
defentry {
  "cnoun" {Agrs: ($ masc &amp; third &amp; pl &amp; (nom|gen|acc))
           Word: "maenner"}}
defentry {
  "cnoun" {Agrs: ($ masc &amp; third &amp; pl &amp; dat)
           Word: "maennern"}}

% Proper names
defentry {
  "pname" {Agrs: ($ fem &amp; sg &amp; third &amp; (nom|dat|acc))
           Word: "maria"}}
defentry {
  "pname" {Agrs: ($ fem &amp; sg &amp; third &amp; gen)
           Word: "marias"}}

defentry {
  "pname" {Agrs: ($ masc &amp; sg &amp; third &amp; (nom|dat|acc))
           Word: "peter"}}
defentry {
  "pname" {Agrs: ($ masc &amp; sg &amp; third &amp; gen)
           Word: "peters"}}

defentry {
  "pname" {Agrs: ($ masc &amp; sg &amp; third)
           Word: "hans"}}

% Finite verbs
defentry {
  "fin" {Agrs: ($ third &amp; sg &amp; nom)
         Word: "wird"}
  "vbsec"}

defentry {
  "fin" {Agrs: ($ third &amp; sg &amp; nom)
         Word: "hat"}
  "vprtc"}

defentry {
  "fin" {Agrs: ($ third &amp; sg &amp; nom)
         Word: "versucht"}
  "vinfc"}

defentry {
  "fin" {Agrs: ($ third &amp; sg &amp; nom)
         Word: "liebt"}
  "tr"}

% Non-finite verbs
defentry {
  "can" {Word: "lieben"}
  "tr"
  "vbse"}
defentry {
  "can" {Word: "geliebt"}
  "tr"
  "vprt"}

defentry {
  "can" {Word: "koennen"}
  "vbsec"
  "vbse"}
defentry {
  "noncan" {Word: "koennen"}
  "vbsec"
  "ersatz"}

defentry {
  "noncan" {Word: "haben"}
  "vprtc"
  "vbse"}

defentry {
  "can" {Word: "lieben"}
  "tr"
  "vinf"}
defentry {
  "noncan" {Word: "lieben"}
  "tr"
  "vinf"}

% zu-particle
defentry {
  "prt" {Word: "zu"}}
</pre>
<!-- ************************************************************ -->

   </body></html>

